plugins {
    // Apply the scala Plugin to add support for Scala.
    id 'scala'
    id 'idea'
    id 'application'
}

repositories {
    // Maven local should contains: libraries, and books
    mavenLocal()
    // Use Maven Central for resolving dependencies (e.g scala-library)
    mavenCentral()
}

dependencies {
    // Use Scala 2.13 in our library project
    implementation 'org.scala-lang:scala-library:2.13.8'
    implementation 'com.tfedorov.gradledependencies:softwarelibrary:0.1'
    implementation 'com.tfedorov.gradledependencies:sciencelibrary:0.1'
    runtimeOnly("org.slf4j:slf4j-simple:2.0.17")
}

application {
    mainClass = 'com.tfedorov.gradledependencies.client.ClientApp'
}

configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'com.tfedorov.gradledependencies' && details.requested.name == 'booktransitive') {
            def overrideVersion = System.getenv("BOOK_TRANSITIVE_VERSION")
            if (overrideVersion) {
                resolutionStrategy.force "com.tfedorov.gradledependencies:booktransitive:$overrideVersion"
                println "Using booktransitive version: $overrideVersion"
            }
        }
    }
}
